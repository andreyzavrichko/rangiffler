plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id "io.freefair.lombok" version '8.12.1'
    id 'com.google.cloud.tools.jib' version '3.4.1'
}

group = 'org.rangiffler'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':rangiffler-grpc-common')
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "net.devh:grpc-server-spring-boot-starter:${project.ext.springGrpcVersion}"

    // Работа с базой
    runtimeOnly "org.postgresql:postgresql:${project.ext.postgresDriverVersion}"
    implementation "org.flywaydb:flyway-core:${project.ext.flywayVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${project.ext.flywayVersion}"

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Тестирование
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.grpc:grpc-testing:1.45.0'
}

jib {
    container {
        ports = ['9000']
        jvmFlags = ["-Dspring.profiles.active=${System.env.PROFILE}"]
        environment = ['VAULT_TOKEN': "${System.env.VAULT_TOKEN}".toString()]
        creationTime = 'USE_CURRENT_TIMESTAMP'
        labels = [
                'maintainer': 'Dmitrii Tuchs @dtuchs',
                'version'   : "${project.version}".toString()
        ]
    }
    from {
        image = "${project.ext.dockerImage}"
        platforms {
            platform {
                architecture = "${project.ext.dockerArch}"
                os = 'linux'
            }
        }
    }
    to {
        image = "${project.ext.dockerHubName}/${project.name}-${System.env.PROFILE}"
        tags = ['latest', "${project.version}"]
    }
}

tasks.jib.dependsOn test
tasks.jibDockerBuild.dependsOn test

tasks.named('test') {
    useJUnitPlatform()
}